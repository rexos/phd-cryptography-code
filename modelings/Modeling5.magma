/*
Quadratic modeling for the bounded syndrome decoding problem over F_q
*/

//problem parameters
q := 7;
F := GF(q);
n := 15;
k := 9;
t := 3;

//code
C := RandomLinearCode(F, n, k);
H := ParityCheckMatrix(C);


//choose whether to force a solution
force := true;

if force then
	//error vector
	e := [F ! 0 : i in [1..n]];
	//support
	E := RandomSubset({1..n}, t);
	for i in E do
	    e[i] := Random(Set(F) diff {0});
	end for;
	e := Vector(e);
	e;
	//syndrome corresponding to the error vector
	s := e*Transpose(H);
else
	//random syndrome
	s := Vector([Random(F) : i in [1..n-k]]);
end if;

//modeling parameters
r1 := 1;
r2 := 2;
m := r1*r2;
qr1 := q^r1;
qr2 := q^r2;
qm := q^m;
F := GF(q);
Fr1 := GF(qr1);
Fr2 := GF(qr2);
Fm := GF(qm);

a := PrimitiveElement(Fm);
f := MinimalPolynomial(a, Fr1);

//Polynomial Ring in n(r_2+2) variables
R := PolynomialRing(Fm, n*(r2+2), "grevlex");
//companion matrix
A := Matrix(R, Transpose(CompanionMatrix(f)));

//MODELING
//support constraint equations
sce := [R.i*(1-R.(n+i)) : i in [1..n]] cat [R.(n+i)^2-R.(n+i) : i in [1..n]];
//hamming-weight computation encoding
hwce := Eltseq(Matrix(1, [R.(2*n+i) : i in [1..r2]])-(1-R.(n+1))*Matrix(1, [R ! 1] cat [0 : i in [2..r2]])-R.(n+1)*A*Matrix(1, [R ! 1] cat [0 : i in [2..r2]])) cat &cat[Eltseq(Matrix(1, [R.(2*n+j*r2+i) : i in [1..r2]])-(1-R.(n+j+1))*Matrix(1, [R.(2*n+(j-1)*r2+i) : i in [1..r2]])-R.(n+j+1)*A*Matrix(1, [R.(2*n+(j-1)*r2+i) : i in [1..r2]])) : j in [1..n-1]];
//weight constraint encoding
wce := Eltseq(Matrix(1, [R.(2*n+(n-1)*r2+i) : i in [1..r2]])-A^t*Matrix(1, [R ! 1] cat [0 : i in [2..r2]]));
//parity-check equations
pce := [&+[H[i,j]*R.j : j in [1..n]]-s[i] : i in [1..n-k]];
//all equations
all := sce cat hwce cat wce cat pce;

SetVerbose("Faugere", 0);
I := Ideal(all);
time GB := GroebnerBasis(I);
dim, free := Dimension(I);
dim, free;

if dim eq 0 then
	var := Variety(I); 
	"#sols = ", #var; "--------";
	for i in [1..#var] do
		"e = ", Vector([F ! var[i][j] : j in [1..n]]);
		"wt = ", #[j : j in [1..n] | var[i][j] ne 0];
		"s = ", Vector([var[i][j] : j in [1..n]])*Transpose(H); 
		"----";
	end for;
end if;

